subprojects {
  apply plugin: 'com.github.harbby.gradle.serviceloader'
  serviceLoader {
    serviceInterface 'ideal.sylph.spi.Runner'
    serviceInterface 'ideal.sylph.etl.PipelinePlugin'
  }

  dependencies {
    compile(project(":sylph-etl-api"))
  }

  def modules = project(':sylph-dist').buildDir.path + "/modules/${name}"
  task copyConf(type: Copy) {
    from('conf')
    into modules + "/conf"
  }
  task copyLibs(type: Copy, dependsOn: copyConf) {
    from(configurations.runtime)
    into modules + "/lib"
    //include   '*.jar'
  }

  task buildModules(type: Copy, dependsOn: copyLibs) {
    from(jar)
    into modules
  }
  assemble.dependsOn buildModules
  project(':sylph-dist').dist.dependsOn assemble
}